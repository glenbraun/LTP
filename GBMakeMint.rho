//scalapackage coop.rchain.rholang.mint

//based on http://erights.org/elib/capability/ode/ode-capabilities.html#simple-money
//requires NonNegativeNumber

//Registry info:
//  sk: a300690f29ac6385917cb94bf534f9b4163792ef8636c5db44608a77fa0356c2
//  pk: d9ba2075d355755060205605f4cdbd5ecd3cce5ed1f39690f34772f7c9aa30ab
//  user == pk
//  timestamp: 1539969637029

//Resulting unforgable name: Unforgeable(0xdf2886c69e880d611d9c609d5ea51a59e43a684f3a587b233be034dace00d092)
//  ==> signature data == 2a3eaa013b0a0d2a0b10feffffffffffffffff010a2a5a280a243a220a20df2886c69e880d611d9c609d5ea51a59e43a684f3a587b233be034dace00d0921001
//  ==> signature == 36229e3f4530c15f3b7c1d9165369201b70b4673289a003652af14b436b20a275d5909d6dfbbd06e685292d39eadf3af11db6f882dcc78ef0b794e6da0ad6109
//URI derived from pk == `rho:id:exunyijimapk7z43g3bbr69awqdz54kyroj9q43jgu3dh567fxsftx`

new
  MakeMint, rs(`rho:registry:insertSigned:ed25519`), uriOut,
  rl(`rho:registry:lookup`), NonNegativeNumberCh
in {

  // GB: Calls registry lookup to get NonNegativeNumberCh
  rl!(`rho:id:nd74ztexkao5awjhj95e3octkza7tydwiy7euthnyrt5ihgi9rj495`, *NonNegativeNumberCh) |
  for(@(_, NonNegativeNumber) <- NonNegativeNumberCh) {

    // GB: MakeMint is now unforgable name
    // GB: 'return' is ... TODO
    contract MakeMint(return) = {
        
      // GB: thisMint is ... TODO
      // GB: internalMakePurse is ... TODO
      // GB: decr is ... TODO
      new thisMint, internalMakePurse, decr in {

        // GB: @"makePurse" is the method
        // GB: @init is ... TODO
        // GB: return is ... TODO (different from 'return' above.)
        contract thisMint(@"makePurse", @init, return) = {
          // GB: balanceCh, holds the balance for this purse
          new balanceCh in {
            // GB: Calls NonNegativeNumber, sends max(0, init) on balanchCh
            @NonNegativeNumber!(init, *balanceCh) | for(@balance <- balanceCh) {
              // GB: Call internal make purse with balance (that is sure to be >= 0)
              internalMakePurse!(balance, *return)
            }
          }
        } |

        // GB: balance is the beginning balance
        // GB: return is ... TODO
        contract internalMakePurse(balance, return) = {

          //balance must be a name that NonNegativeNumber contract is listening on
          new thisPurse in {
            // GB: Methods for the purse
            // GB: decr is an unforgeable name used as a method name
            //     only holders of this decr name, and the purse can subtract from it
            //     NonNegativeNumber implements the "sub" method, insures balance >= 0
            //     success = true when amount <= balance and amount >= 0
            contract thisPurse(@=*decr, @amount, success) = { balance!("sub", amount, *success) } |

            // GB: Retrieves value from cell
            contract thisPurse(@"getBalance", return) = { balance!("value", *return) } |

            // GB: Create a new purse with zero balance but from same mint
            contract thisPurse(@"sprout", return) = { thisMint!("makePurse", 0, *return) } |

            // GB: Creates a new purse in this mint
            //     Fills new purse from an amount specified
            //     These funds come from this purse
            contract thisPurse(@"split", @amount, return) = {

              // GB: destPurchCh is the new purse created from sprout
              // GB: successCh is local channel for success of the deposit
              new destPurseCh, successCh in {

                // GB: Make the new purse, with 0 balance
                // GB: Hold reference in destPurse
                thisPurse!("sprout", *destPurseCh) | for(@destPurse <- destPurseCh) {

                  // GB: Try to deposit the amount of the split into the destPurse
                  //     Funds come from this purse
                  @destPurse!("deposit", amount, *thisPurse, *successCh) |

                  // GB: depost fails if this purse does not have enough to withdraw
                  for(@success <- successCh) {

                    if (success) {
                      // GB: return destPurse in a list. 
                      //     TODO Why use a list?
                      return!([destPurse])
                    } else {
                      // GB: return empty list indicating failure. 
                      //     TODO Why empty list rather than maybe Nil?
                      return!([])
                    }
                  }
                }
              }
            } |
            
            // GB: deposit method for this purse
            //     Transfers amount from the source purse to this purse
            contract thisPurse(@"deposit", @amount, @src, success) = {
              new result in {
                // GB: Try to remove the amount from the source purse
                @src!(*decr, amount, *result) | //call src decr function.

                for(@decrSuccess <- result) {
                  if (decrSuccess) {
                    // GB: If we were able to withdraw from source purse, then add to this purse
                    balance!("add", amount, *success) // add transferred amount to this purse
                  } else {
                    // GB: else, return false
                    success!(false) 
                  }
                }
              }
            } |

            // GB: return this purse with only send capability to client
            return!(bundle+{*thisPurse})
          }
        } |

        // GB: return this mint with only send capability to client
        return!(bundle+{*thisMint})
      }
    }
  } |

  // GB: Register the MakeMint contract in the registry
  rs!(
    "d9ba2075d355755060205605f4cdbd5ecd3cce5ed1f39690f34772f7c9aa30ab".hexToBytes(), 
    (9223372036854775807, bundle+{*MakeMint}), 
    "36229e3f4530c15f3b7c1d9165369201b70b4673289a003652af14b436b20a275d5909d6dfbbd06e685292d39eadf3af11db6f882dcc78ef0b794e6da0ad6109".hexToBytes(), 
    *uriOut
  )

}